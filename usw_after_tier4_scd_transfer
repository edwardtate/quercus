create or replace TRIGGER quercus.usw_tier4_audit_transfer
-- ================================================================================
-- Trigger to create a T4_TRANSFER object note against the student_course_detail 
-- where this is a TIER4 enrolment.
--
-- Created by: Ted Tate, 27 July 2016.
-- ================================================================================
 AFTER
 INSERT ON audit_transfer 
 FOR EACH ROW 
 
 DECLARE 
    
        v_course_code           course$.course%TYPE;
        v_course_description    course$.description%TYPE;
        v_academic_year         VARCHAR2 (16) ;
        v_instance_code         course_instance_table.instance_code%TYPE;
        v_note                  object_note.note%TYPE;
        v_timestamp             VARCHAR2 (50) ;
        v_crlf                  VARCHAR2 (16) := chr (13) ||chr (10) ;
        v_tagline               VARCHAR2 (80) := 'Note created by trigger: usw_tier4_audit_transfer.';
        v_object_note_type      object_note_type.object_note_type%TYPE := 'T4_TRANSFER';
        v_t4_count              number := 0;
        
BEGIN

        -- Count the number of current CAS or VISATIER4 reference numbers
        SELECT COUNT(prn.object_id)
          INTO v_t4_count
          FROM student_course_detail$ scd,
               PERSON_REFERENCE_NUMBER PRN,
               REFERENCE_NUMBER_TYPE RNT
         WHERE PRN.PERSON = SCD.PERSON
           AND RNT.OBJECT_ID = PRN.REFERENCE_NUMBER_TYPE
           AND RNT.REFERENCE_NUMBER_TYPE IN ('CAS', 'VISATIER4')
           AND SYSDATE >= NVL (PRN.START_DATE, SYSDATE - 1) 
           AND SYSDATE <= NVL (PRN.END_DATE, SYSDATE + 1)
           AND scd.object_id = :NEW.student_course_detail; 


    IF v_t4_count > 0 THEN

        --
        -- Get details for the transferred to course instance .  
     SELECT c.course,
            c.description,
            ci.INSTANCE_CODE,
            TO_CHAR (ci.academic_year)
       INTO v_course_code,
            v_course_description,
            v_instance_code,
            v_academic_year
       FROM course_instance_table ci,
            course$ c
      WHERE c.object_id = ci.course
        AND ci.object_id = :new.course_instance;
        
        
        SELECT to_char(sysdate,'HH24:mi:ss fmDay ddth Month YYYY. ')
        INTO v_timestamp
        from dual;

        --
        -- Create the note text.
        IF v_course_code IS NOT NULL THEN
            v_note := 'Student transferred enrolment to course '|| v_crlf ||
            v_course_description ||' '|| v_crlf || v_course_code ||' / '||v_instance_code ||
            ' in session '|| v_academic_year ||'.'|| v_crlf || v_tagline;

        ELSE
            v_note := 'Student transferred enrolment. No destination course instance was specified!'||
                      v_crlf || v_tagline;

        END IF;
        
        --
        -- Create an object note against the transferred from course enrolment.
        qp_note.set_object_note (p_object_number => :NEW.student_course_detail, 
                                 p_object_note_type => v_object_note_type, 
                                 p_note => v_note) ;                              

     ELSE
        -- Do nothing.
        NULL;

     END IF;


EXCEPTION

-- No course instance specified.
WHEN NO_DATA_FOUND THEN
    NULL;

WHEN OTHERS THEN
    raise_application_error ( - 20001, sqlerrm) ;

END;
